region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack

export AWS_REGION := region
export AWS_PAGER := ""

zip:
    ./gradlew buildZip

[working-directory: "infra"]
deploy: zip
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve \
        -var="bundle_path={{justfile_directory()}}/build/distributions/jvm-spring-lambda.zip"

[working-directory: "infra"]
destroy:
    #!/bin/bash -e
    tmp=$(mktemp)
    {{tf-bin}} destroy -auto-approve -var="bundle_path=${tmp}"
    rm -f "$tmp"