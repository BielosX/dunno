region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack

export AWS_REGION := region

clean:
    rm -rf bin

build: clean
    mkdir -p bin
    GOOS=linux GOARCH=arm64 go build -ldflags "-s -w" -o bin/bootstrap main.go

[working-directory: "bin"]
zip: build
    zip bootstrap.zip bootstrap

[working-directory: "infra/lambda"]
deploy-lambda: zip
    {{tf-bin}} init
    {{tf-bin}} apply -auto-approve -var="bundle_path={{justfile_directory()}}/bin/bootstrap.zip"

[working-directory: "infra/opensearch"]
deploy-opensearch:
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve

[working-directory: "infra/opensearch"]
destroy-opensearch:
    {{tf-bin}} destroy -auto-approve

[working-directory: "infra/lambda"]
destroy-lambda:
    #!/bin/bash -e
    tmp=$(mktemp)
    {{tf-bin}} destroy -auto-approve \
        -var="bundle_path=${tmp}"
    rm -f "${tmp}"

destroy-all: destroy-opensearch destroy-lambda
deploy-all: deploy-lambda deploy-opensearch