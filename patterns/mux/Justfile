region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack

export AWS_REGION := region

clean:
    rm -f bootstrap
    rm -f *.zip

build: clean
    GOOS=linux GOARCH=arm64 go build -o bootstrap main.go

zip: build
    zip bootstrap.zip bootstrap

[working-directory: "infra"]
deploy: zip
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve -var="file_name={{justfile_directory()}}/bootstrap.zip"

[working-directory: "infra"]
destroy:
    #!/bin/bash -e
    tmp=$(mktemp)
    {{tf-bin}} destroy -auto-approve -var="file_name=${tmp}"
    rm -f "${tmp}"

