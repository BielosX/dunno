region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack

export AWS_REGION := region

clean:
    rm -f bootstrap
    rm -f *.zip

[working-directory: "gorilla-mux"]
gorilla-build: clean
    GOOS=linux GOARCH=arm64 go build -o bootstrap main.go

[working-directory: "gorilla-mux"]
gorilla-zip: gorilla-build
    zip bootstrap.zip bootstrap

[working-directory: "infra"]
deploy: gorilla-zip
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve \
        -var="gorilla_bundle_path={{justfile_directory()}}/gorilla-mux/bootstrap.zip"

[working-directory: "infra"]
destroy:
    #!/bin/bash -e
    tmp=$(mktemp)
    {{tf-bin}} destroy -auto-approve -var="gorilla_bundle_path=${tmp}"
    rm -f "${tmp}"

