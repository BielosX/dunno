region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack

export AWS_REGION := region

clean:
    rm -f bootstrap
    rm -f *.zip

[working-directory: "gorilla-mux"]
gorilla-build: clean
    GOOS=linux GOARCH=arm64 go build -o bootstrap main.go

[working-directory: "gorilla-mux"]
gorilla-zip: gorilla-build
    zip bootstrap.zip bootstrap

[working-directory: "python-powertools"]
python-powertools-build:
    rm -rf build-arm64
    mkdir -p build-arm64/
    uv venv
    source .venv/bin/activate
    uv sync && uv export --no-hashes --no-header --no-annotate --no-dev --format requirements-txt > requirements.txt
    uv run python -m ensurepip --upgrade
    uv run python -m pip install --upgrade pip
    python -m pip install -r requirements.txt \
        --platform manylinux2014_aarch64 \
        --only-binary=:all: \
        --target build-arm64/
    cp main.py build-arm64/

[working-directory: "python-powertools/build-arm64"]
python-powertools-zip: python-powertools-build
    zip -r ../lambda-arm64.zip .

[working-directory: "rust-axum"]
rust-axum-build:
    cargo lambda build --release --output-format zip --target "aarch64-unknown-linux-gnu"

[working-directory: "infra"]
deploy: gorilla-zip python-powertools-zip rust-axum-build
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve \
        -var="gorilla_bundle_path={{justfile_directory()}}/gorilla-mux/bootstrap.zip" \
        -var="python_powertools_path={{justfile_directory()}}/python-powertools/lambda-arm64.zip" \
        -var="rust_axum_path={{justfile_directory()}}/rust-axum/target/lambda/dunno/bootstrap.zip"

[working-directory: "infra"]
destroy:
    #!/bin/bash -e
    tmp=$(mktemp)
    {{tf-bin}} destroy -auto-approve \
        -var="gorilla_bundle_path=${tmp}" \
        -var="python_powertools_path=${tmp}" \
        -var="rust_axum_path=${tmp}"
    rm -f "${tmp}"

