region := env("AWS_REGION", "eu-central-1")
tf-bin := env("TF_BIN", "tofu") # for tofu tflocal: TF_BIN=tflocal TF_CMD=tofu
aws-cli := env("AWS_CLI", "aws") # awslocal for localstack
lambda-count := env("LAMBDA_COUNT", "1")
lambda-amd64-arch := "x86_64"
lambda-arm64-arch := "arm64"
lambda-arch := env("LAMBDA_ARCH", "x86_64")

export AWS_REGION := region

[working-directory: "rust"]
rust-clean:
    cargo clean

[working-directory: "rust"]
rust-fmt:
    cargo fmt

[working-directory: "rust"]
rust-build: rust-clean
    #!/bin/bash -e
    case "{{lambda-arch}}" in
      "{{lambda-amd64-arch}}")
        cargo lambda build --release --output-format zip --target "x86_64-unknown-linux-gnu"
        ;;
      "{{lambda-arm64-arch}}")
        cargo lambda build --release --output-format zip --target "aarch64-unknown-linux-gnu"
        ;;
      *)
        echo "Arch {{lambda-arch}} not supported"
        ;;
    esac

[working-directory: "infra/artifacts"]
artifacts-apply:
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve

[working-directory: "infra/artifacts"]
artifacts-bucket:
    {{tf-bin}} output -raw bucket_name

[working-directory: "infra/artifacts"]
artifacts-destroy:
    {{tf-bin}} destroy -auto-approve

[working-directory: "infra/lambda"]
lambda-apply language handler bucket bucket_key count snap_start="false":
    {{tf-bin}} init && {{tf-bin}} apply -auto-approve \
        -var="handler={{handler}}" \
        -var="language={{language}}" \
        -var="bucket={{bucket}}" \
        -var="architecture={{lambda-arch}}" \
        -var="bucket_key={{bucket_key}}" \
        -var="lambda_count={{count}}" \
        -var="snap_start={{snap_start}}"

[working-directory: "infra/lambda"]
destroy-lambda:
    {{tf-bin}} destroy -auto-approve

[working-directory: "golang"]
golang-clean:
    rm -f bootstrap
    rm -f *.zip

[working-directory: "golang"]
golang-build:
    #!/bin/bash -e
    case "{{lambda-arch}}" in
      "{{lambda-arm64-arch}}")
        GOOS=linux GOARCH=arm64 go build -o bootstrap main.go
        ;;
      "{{lambda-amd64-arch}}")
        GOOS=linux GOARCH=amd64 go build -o bootstrap main.go
        ;;
      *)
        echo "Arch {{lambda-arch}} not supported"
        ;;
    esac

[working-directory: "dotnet"]
dotnet-package:
    dotnet lambda package -farch "{{lambda-arch}}"

[working-directory: "golang"]
golang-zip: golang-clean golang-build
    zip dunno.zip bootstrap

[working-directory: "java"]
java-clean:
    ./gradlew clean

[working-directory: "java"]
java-zip: java-clean
    ./gradlew buildZip

[working-directory: "python"]
python-clean:
    rm -f *.zip

[working-directory: "ruby"]
ruby-clean:
    rm -f *.zip

[working-directory: "js"]
js-clean:
    rm -f *.zip

[working-directory: "js"]
js-zip:
    zip dunno.zip main.js

[working-directory: "python"]
python-zip: python-clean
    zip dunno.zip main.py

[working-directory: "ruby"]
ruby-zip: ruby-clean
    zip dunno.zip main.rb

snapstart-status name alias:
    {{aws-cli}} lambda get-function-configuration --function-name "{{name}}" --qualifier "{{alias}}" \
        | jq -r '{SnapStart: .SnapStart, State: .State}'

deploy-lambda language snap_start="false":
    #!/bin/bash -e
    just artifacts-apply
    bucket=$(just artifacts-bucket)
    timestamp=$(date +%s)
    bucket_key="dunno-${timestamp}.zip"
    export TF_VAR_architecture="{{lambda-arch}}"
    case "{{language}}" in
      "golang")
        just golang-clean
        just golang-zip
        {{aws-cli}} s3 cp golang/dunno.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "golang" "dummy" "${bucket}" "${bucket_key}" "{{lambda-count}}"
        ;;
      "java")
        just java-zip
        {{aws-cli}} s3 cp java/build/distributions/dunno.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "java" "org.dunno.Handler" "${bucket}" "${bucket_key}" "{{lambda-count}}" "{{snap_start}}"
        ;;
      "python")
        just python-zip
        {{aws-cli}} s3 cp python/dunno.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "python" "main.handler" "${bucket}" "${bucket_key}" "{{lambda-count}}" "{{snap_start}}"
        ;;
      "rust")
        just rust-build
        {{aws-cli}} s3 cp rust/target/lambda/dunno/bootstrap.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "rust" "dummy" "${bucket}" "${bucket_key}" "{{lambda-count}}"
        ;;
      "dotnet")
        just dotnet-package
        {{aws-cli}} s3 cp dotnet/bin/Release/net8.0/dotnet.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "dotnet" "dunno::dunno.Function::FunctionHandler" \
            "${bucket}" "${bucket_key}" "{{lambda-count}}" "{{snap_start}}"
        ;;
      "ruby")
        just ruby-zip
        {{aws-cli}} s3 cp ruby/dunno.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "ruby" "main.handler" "${bucket}" "${bucket_key}" "{{lambda-count}}"
        ;;
      "js")
        just js-zip
        {{aws-cli}} s3 cp js/dunno.zip "s3://${bucket}/${bucket_key}"
        just lambda-apply "js" "main.handler" "${bucket}" "${bucket_key}" "{{lambda-count}}"
        ;;
      *)
        echo "Wrong language name"
        ;;
    esac