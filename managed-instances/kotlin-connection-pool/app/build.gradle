import org.flywaydb.gradle.task.AbstractFlywayTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:11.20.0")
    }
}

plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.spotless)
    alias(libs.plugins.flyway)
    id 'application'
}

repositories {
    mavenCentral()
}

spotless {
    kotlin {
        ktfmt()
    }
}

dependencies {
    implementation(platform("org.http4k:http4k-bom:6.25.1.0"))
    implementation(platform("software.amazon.awssdk:bom:2.41.1"))
    implementation("software.amazon.awssdk:ssm")
    implementation("org.http4k:http4k-core")
    implementation("org.http4k:http4k-format-moshi")
    implementation("com.squareup.moshi:moshi-kotlin:1.15.2")
    implementation ("com.zaxxer:HikariCP:7.0.2")

    implementation ("org.postgresql:postgresql:42.7.8")

    implementation("com.amazonaws:aws-lambda-java-core:1.4.0")
    implementation("com.amazonaws:aws-lambda-java-events:3.16.1")

    implementation("software.amazon.awssdk:rds")

    implementation("org.jetbrains.exposed:exposed-core:0.61.0")
    implementation("org.jetbrains.exposed:exposed-jdbc:0.61.0")

    implementation ("org.slf4j:slf4j-api:2.0.17")
    implementation ("ch.qos.logback:logback-classic:1.5.23")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

def dbUser = providers.gradleProperty("dbUser").orElse("test")
def dbHost = providers.gradleProperty("dbHost").orElse("localhost")
def dbPort = providers.gradleProperty("dbPort").map {it.toInteger()}.orElse(5432)
def dbName = providers.gradleProperty("dbName").orElse("postgres")
def dbPassword = providers.gradleProperty("dbPassword").orElse("test")
def dbSslMode = providers.gradleProperty("dbSsl").orElse("disable")

tasks.withType(AbstractFlywayTask).configureEach {
    notCompatibleWithConfigurationCache(
            'Flyway Gradle plugin uses Task.project at execution time'
    )
}

flyway {
    url = "jdbc:postgresql://${dbHost.get()}:${dbPort.get()}/${dbName.get()}?sslmode=${dbSslMode.get()}"
    user = "${dbUser.get()}"
    password = "${dbPassword.get()}"
    baselineOnMigrate = true
}

tasks.register('buildZip', Zip) {
    from compileKotlin
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}